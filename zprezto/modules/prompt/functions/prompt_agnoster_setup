#
# Agnoster theme from https://gist.github.com/3712874
#
# Authors:
# agnoster (original)
# digitalformula (modified for compatibility with Prezto - https://github.com/sorin-ionescu/prezto)
#
# Original agnoster theme was written for Oh My Zsh - https://github.com/robbyrussell/oh-my-zsh
# This modified version requires git.zsh from Oh My Zsh - put in ~/.yadr/zsh for automatic load

# Load dependencies.
pmodload 'helper'

function prompt_agnoster_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
    git-info
  fi
}

# Begin a segment
# Takes two arguments, background and foreground. Both can be omitted,
# rendering default background/foreground.
prompt_segment() {
  local bg fg
  [[ -n $1 ]] && bg="%K{$1}" || bg="%k"
  [[ -n $2 ]] && fg="%F{$2}" || fg="%f"
  if [[ $CURRENT_BG != 'NONE' && $1 != $CURRENT_BG ]]; then
    echo -n " %{$bg%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR%{$fg%} "
  else
    echo -n "%{$bg%}%{$fg%} "
  fi
  CURRENT_BG=$1
  [[ -n $3 ]] && echo -n $3
}

function __my_rvm_ruby_version {
  local gemset=$(echo $GEM_HOME | awk -F'@' '{print $2}')
  [[ "$gemset" != "" ]] && gemset="@$gemset"
  local version=${MY_RUBY_HOME:t}
  local full="$version$gemset"
  [[ "$full" != "" ]] && echo "($full) "
}

# End the prompt, closing any open segments
prompt_end() {
  br="
"
  if [[ -n $CURRENT_BG ]]; then
    echo -n " %{%k%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR"
    echo -n $br
    echo -n "%{%f%}"
    [[ -n $VIRTUAL_ENV ]] && echo -n "(${VIRTUAL_ENV:t}) "
    # echo "$(__my_rvm_ruby_version)❯"
    echo "❯"
  else
    echo -n "%{%k%}"
  fi
  CURRENT_BG=''
}

### Prompt components
# Each component will draw itself, and hide itself if no information needs to be shown

# Context: user@hostname (who am I and where am I)
prompt_context() {
  local user=`whoami`

  if [[ "$user" != "$DEFAULT_USER" || -n "$SSH_CLIENT" ]]; then
    prompt_segment black default "%(!.%{%F{yellow}%}.)$user@%m"
  fi
}


# Git: branch/detached head, dirty status
prompt_git() {
  if $(git rev-parse --is-inside-work-tree >/dev/null 2>&1); then
    col1=green
    # Dirty status
    [[ -n $(git status -s 2>/dev/null) ]] && col1=yellow && icons+=" $SOFT_SEPARATOR ✱"
    # Stash status
    [[ -n $(git stash list 2>/dev/null) ]] && icons+=" $SOFT_SEPARATOR ⚙"
    # Commit status (ahead or not)
    [[ -n $(git status 2>/dev/null| grep "ahead") ]] && icons+=" $SOFT_SEPARATOR ⬆"
    # prompting result : branch and then status icons
    prompt_segment $col1 black "$(git rev-parse --abbrev-ref HEAD)$icons"
  fi
}

# Current Working Directory (shortened version if needed)
prompt_dir() {
  if [[ "`pwd`" == "/" ]]; then
    prompt_segment cyan black "/"
  else
    prompt_segment cyan black `get_short_path | sed "s:/: $SOFT_SEPARATOR :g" | sed "s:^ $SOFT_SEPARATOR ::"`
  fi
}

# Status:
# - was there an error
# - am I root
# - are there background jobs?
prompt_status() {
  local symbols
  symbols=()
  [[ $RETVAL -ne 0 ]] && symbols+="%{%F{red}%}✘"
  [[ $UID -eq 0 ]] && symbols+="%{%F{yellow}%}⚡"
  [[ $(jobs -l | wc -l) -gt 0 ]] && symbols+="%{%F{cyan}%}⚙"
  [[ (-n $VIRTUAL_ENV) ]] && symbols+="%{%F{yellow}%}Ϟ"

  [[ -n "$symbols" ]] && prompt_segment black black "$symbols"
}

prompt_user(){
  prompt_segment black yellow "`whoami`@`hostname -s`"
}

build_prompt() {
  RETVAL=$?
  echo
  prompt_status
  prompt_user
  prompt_git
  prompt_dir
  prompt_end
}

function prompt_agnoster_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  VIRTUAL_ENV_DISABLE_PROMPT=1

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_agnoster_precmd

  zstyle ':prezto:module:editor' completing '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:keymap:primary' overwrite ' %F{red}♺%f'
  zstyle ':prezto:module:editor:keymap' primary ' %B%F{red}❯%F{yellow}❯%F{green}❯%f%b'
  zstyle ':prezto:module:editor:keymap' alternate ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'
  zstyle ':prezto:module:git' action ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git' added ' %%B%F{green}✚%f%%b'
  zstyle ':prezto:module:git' ahead ' %%B%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git' behind ' %%B%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git' branch ':%F{green}%b%f'
  zstyle ':prezto:module:git' commit ':%F{green}%.7c%f'
  zstyle ':prezto:module:git' deleted ' %%B%F{red}✖%f%%b'
  zstyle ':prezto:module:git' modified ' %%B%F{blue}✱%f%%b'
  zstyle ':prezto:module:git' position ':%F{green}%p%f'
  zstyle ':prezto:module:git' renamed ' %%B%F{magenta}➜%f%%b'
  zstyle ':prezto:module:git' stashed ' %%B%F{cyan}✭%f%%b'
  zstyle ':prezto:module:git' unmerged ' %%B%F{yellow}═%f%%b'
  zstyle ':prezto:module:git' untracked ' %%B%F{white}◼%f%%b'
  zstyle ':prezto:module:git' info \
    'prompt' ' %F{blue}git%f$(coalesce "%b" "%p" "%c")%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'
  # Python virtualenv settings
  zstyle ':prezto:module:python:info:virtualenv' format 'py (%v)'

    
# Original theme's comments left intact:

# vim:ft=zsh ts=2 sw=2 sts=2
#
# agnoster's Theme - https://gist.github.com/3712874
# A Powerline-inspired theme for ZSH
#
# # README
#
# In order for this theme to render correctly, you will need a
# [Powerline-patched font](https://gist.github.com/1595572).
#
# In addition, I recommend the
# [Solarized theme](https://github.com/altercation/solarized/) and, if you're
# using it on Mac OS X, [iTerm 2](http://www.iterm2.com/) over Terminal.app -
# it has significantly better color fidelity.
#
# # Goals
#
# The aim of this theme is to only show you *relevant* information. Like most
# prompts, it will only show git information when in a git working directory.
# However, it goes a step further: everything from the current user and
# hostname to whether the last call exited with an error to whether background
# jobs are running in this shell will all be displayed automatically when
# appropriate.

### Segment drawing
# A few utility functions to make it easy and re-usable to draw segmented prompts

CURRENT_BG='NONE'
SEGMENT_SEPARATOR='⮀'
SOFT_SEPARATOR='⮁'

PROMPT='%{%f%b%k%}$(build_prompt) '

}

prompt_agnoster_setup "$@"
